---
title: shiftOut()
categories: [ "Functions" ]
subCategories: [ "Advanced I/O" ]
---
// ARDUINO LANGUAGE REFERENCE TAG (above)   ►►►►► ALWAYS INCLUDE IN YOUR FILE ◄◄◄◄◄

// PAGE TITLE
= shiftOut()

// OVERVIEW SECTION STARTS
[#overview]
--

[float]
=== Descripción
Cambia en un byte de datos, un bit por vez. Comienza desde el bit más significativo (es decir, el más a la izquierda) o menos significativo (más a la derecha). Cada bit se escribe a su vez en un pin de datos, después de lo cual se pulsa un pin de reloj (tomado en `HIGH`, y luego en `LOW`) para indicar que el bit está disponible.


Notas:
[%hardbreaks]
Si está interactuando con un dispositivo que está marcado por bordes de subida, deberá asegurarse de que el pin del reloj esté `LOW` antes de la primera llamada a `shiftIn()`. + 
Ejemplo: con una llamada a `digitalWrite(clockPin, LOW)`.

Esta es una implementación de software; Arduino también proporciona una link:https://www.arduino.cc/en/Reference/SPI[librería SPI] que usa la implementación de hardware, que es más rápida pero solo funciona en pines específicos.


[float]
=== Sintaxis
`shiftOut(dataPin, clockPin, bitOrder, value)`


[float]
=== Parámetros
`dataPin`: Pin en el que sale cada bit - `(int)`

`clockPin`: Pin que cambia una vez que el `dataPin`  se ha establecido en el valor correcto - `(int)`

`bitOrder`: Orden en que se desplazan los bits de salida; ya sea `MSBFIRST` o `LSBFIRST`. (Most Significant Bit First, Least Significant Bit First)

`value`: Datos de salida - `(byte)`


[float]
=== Retorno
Nada

--
// OVERVIEW SECTION ENDS


// HOW TO USE SECTION STARTS
[#howtouse]
--

[float]
=== Código de ejemplo
// Describe what the example code is all about and add relevant code   ►►►►► THIS SECTION IS MANDATORY ◄◄◄◄◄
Para conocer el circuito mire el tutorial http://arduino.cc/en/Tutorial/ShiftOut[controlling a 74HC595 shift register].

[%hardbreaks]
// CODE
[source,arduino]
----
//**************************************************************//
//  Name    : shiftOutCode, Hello World                         //
//  Author  : Carlyn Maw,Tom Igoe                               //
//  Date    : 25 Oct, 2006                                      //
//  Version : 1.0                                               //
//  Notes   : Code for using a 74HC595 Shift Register           //
//          : to count from 0 to 255                            //
//****************************************************************

//Pin connected to ST_CP of 74HC595
int latchPin = 8;
//Pin connected to SH_CP of 74HC595
int clockPin = 12;
////Pin connected to DS of 74HC595
int dataPin = 11;

void setup() {
  //set pins to output because they are addressed in the main loop
  pinMode(latchPin, OUTPUT);
  pinMode(clockPin, OUTPUT);
  pinMode(dataPin, OUTPUT);
}

void loop() {
  //count up routine
  for (int j = 0; j < 256; j++) {
    //ground latchPin and hold low for as long as you are transmitting
    digitalWrite(latchPin, LOW);
    shiftOut(dataPin, clockPin, LSBFIRST, j);
    //return the latch pin high to signal chip that it
    //no longer needs to listen for information
    digitalWrite(latchPin, HIGH);
    delay(1000);
  }
}
----
[%hardbreaks]


[float]
=== Notas y advertencias
El `dataPin` y el `clockPin` deben estar configurados como salidas mediante una llamada a link:../../digital-io/pinmode[pinMode()].

`shiftOut` se escribe en el byte de salida 1 (8 bits) por lo que se requiere una operación de dos pasos para generar valores superiores a 255.

// CODE
[source,arduino]
----
// Do this for MSBFIRST serial
int data = 500;
// shift out highbyte
shiftOut(dataPin, clock, MSBFIRST, (data >> 8));
// shift out lowbyte
shiftOut(dataPin, clock, MSBFIRST, data);

// Or do this for LSBFIRST serial
data = 500;
// shift out lowbyte
shiftOut(dataPin, clock, LSBFIRST, data);
// shift out highbyte
shiftOut(dataPin, clock, LSBFIRST, (data >> 8));
----

--
// HOW TO USE SECTION ENDS
